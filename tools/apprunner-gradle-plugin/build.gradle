plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.12.0'
  id "com.diffplug.spotless" version "5.12.5"
}

def readVersion(expr) {
  def buffer = new ByteArrayOutputStream()
  exec {
    commandLine = ['../../mvnw', 'help:evaluate', "-Dexpression=$expr", '-q', '-DforceStdout', '-f', '../../pom.xml']
    standardOutput = buffer
    environment('JAVA_HOME', System.properties['java.home'])
  }.assertNormalExitValue()
  return buffer.toString()
}

def junitVersion = readVersion('junit.version')
def jacksonVersion = readVersion('jackson.version')
def assertjVersion = readVersion('assertj.version')
version = readVersion('project.version')

allprojects {
  group = "org.projectnessie"
  version = version
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

spotless {
  java {
    googleJavaFormat()
    licenseHeaderFile(file("../../codestyle/copyright-header-java.txt"))
  }
}

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'org.projectnessie'
      implementationClass = 'org.projectnessie.quarkus.gradle.QuarkusAppPlugin'
      displayName = 'Quarkus App Runner'
      description = 'Start and stop a quarkus app as gradle tasks for integration testing'
    }
  }
}

repositories {
  mavenCentral()
  if (version.endsWith("-SNAPSHOT")) {
    mavenLocal()
  }
}

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

test {
  systemProperties([
          'nessie-version': version,
          'junit-version': junitVersion,
          'jackson-version': jacksonVersion
  ])
  useJUnitPlatform()
}

dependencies {
  compile "org.projectnessie:nessie-apprunner-common:$version"

  testCompile gradleTestKit()
  testCompile("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testCompile("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
  testCompile("org.assertj:assertj-core:${assertjVersion}")
}

pluginBundle {
  website = 'https://projectnessie.org'
  vcsUrl = 'https://github.com/projectnessie/nessie'
  tags = ['test', 'integration', 'quarkus']
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'org.projectnessie'
      artifactId = 'nessie-apprunner-gradle-plugin'
      version = version

      from components.java
    }
  }
}
